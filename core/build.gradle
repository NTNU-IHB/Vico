plugins {
    id 'kotlin'
    id 'java-library'
}

apply from: rootProject.file("gradle/mavenpublish.gradle")
apply from: rootProject.file("gradle/bintraypublish.gradle")

dependencies {
    api "org.joml:joml:1.9.25"
    api "info.laht.krender:render-api:$krender_version"

    testImplementation project(':dsl')
    testImplementation project(':fmi')
    testImplementation project(':chart')
    testImplementation project(':test-data')
    //testImplementation "info.laht.krender:jme-render:$krender_version"
    testImplementation "info.laht.krender:threekt-render:$krender_version"
}


def generatedSourceDir = new File(buildDir, "generated/src/main/kotlin")
sourceSets.main.kotlin.srcDirs += generatedSourceDir.absolutePath

task injectVersion() {
    doLast {
        def packageFolder = project.group.replace(".", "/")
        def versionFile = new File(generatedSourceDir, "$packageFolder/version.kt")
        if (!versionFile.parentFile.exists()) {
            versionFile.parentFile.mkdirs()
            versionFile.createNewFile()
        }
        versionFile.write(
                "package ${project.group}\n\n" +
                "object Vico {\n\n" +
                "   @JvmField\n" +
                "   val version = \"${project.version}\"\n\n" +
                "}\n"
        )

    }
}

compileKotlin.dependsOn injectVersion

