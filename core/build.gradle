plugins {
    id 'kotlin'
    id 'java-library'
}

apply from: rootProject.file("gradle/publisher.gradle")

dependencies {
    api "org.joml:joml:1.9.25"

    implementation group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.1'
    implementation group: 'org.glassfish.jaxb', name: 'jaxb-core', version: '2.3.0.1'
    implementation group: 'org.glassfish.jaxb', name: 'jaxb-runtime', version: '2.3.3'

    testImplementation project(":web")
    testImplementation project(':fmi')
    testImplementation project(':chart')
    testImplementation project(':test-data')
    testImplementation project(":threekt-render")
    testImplementation('info.laht.kts:host:0.4.2')

}


def generatedSourceDir = new File(buildDir, "generated/src/main/kotlin")
sourceSets.main.kotlin.srcDirs += generatedSourceDir.absolutePath

task injectVersion() {
    doLast {
        def packageFolder = "no/ntnu/ihb/vico"
        def versionFile = new File(generatedSourceDir, "$packageFolder/version.kt")
        if (!versionFile.parentFile.exists()) {
            versionFile.parentFile.mkdirs()
            versionFile.createNewFile()
        }
        versionFile.write(
                "package no.ntnu.ihb.vico\n\n" +
                "object Vico {\n\n" +
                "   @JvmField\n" +
                "   val version = \"${project.version}\"\n\n" +
                "}\n"
        )

    }
}

compileKotlin.dependsOn injectVersion

