plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.4-M2' apply(false)
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
    gradleVersion = '6.4'
}

version = rootProject.file("VERSION").readLines()[0]
println("${rootProject.name} v$version")

subprojects { sub ->

    apply plugin: 'java-library'

    sub.group = 'no.ntnu.ihb.acco'
    sub.version = version

    repositories {
        mavenCentral()
        maven { url "https://dl.bintray.com/ntnu-ihb/mvn" }
        maven { url "https://dl.bintray.com/kotlin/kotlin-eap/" }
        maven { url  "https://dl.bintray.com/open-simulation-platform/maven" }
        maven { url "https://dl.bintray.com/jmonkeyengine/org.jmonkeyengine" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    }

    dependencies {
        def slf4j_version = '1.7.25'
        implementation group: 'org.slf4j', name: 'slf4j-api', version: slf4j_version
        runtimeOnly group: 'org.slf4j', name: 'slf4j-log4j12', version: slf4j_version

        def junit_version = '5.3.2'
        testImplementation("org.junit.jupiter:junit-jupiter-api:$junit_version")
        testImplementation("org.junit.jupiter:junit-jupiter-params:$junit_version")
        testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:$junit_version")
    }

    test {
        failFast = true
        useJUnitPlatform()
    }

    pluginManager.withPlugin('kotlin') {

        dependencies {
            implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
        }

        compileKotlin {
            kotlinOptions.jvmTarget = "1.8"
        }
        compileTestKotlin {
            kotlinOptions.jvmTarget = "1.8"
        }

    }

}

