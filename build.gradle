plugins {
    id 'base'
    id 'org.jetbrains.kotlin.jvm' version '1.4.32' apply(false)
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
    gradleVersion = '7.0'
}

version = rootProject.file("VERSION").readLines()[0]

println "Gradle version is ${gradle.getGradleVersion()}"
println("Building ${rootProject.name} v$version")

subprojects { sub ->

    apply plugin: 'java'

    sub.group = 'info.laht.vico'
    sub.version = rootProject.version

    repositories {
        mavenCentral()
    }

    dependencies {
        def slf4j_version = '1.7.30'
        implementation "org.slf4j:slf4j-api:$slf4j_version"
        runtimeOnly "org.slf4j:slf4j-log4j12:$slf4j_version"

        def junit_version = '5.6.2'
        testImplementation("org.junit.jupiter:junit-jupiter-api:$junit_version")
        testImplementation("org.junit.jupiter:junit-jupiter-params:$junit_version")
        testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:$junit_version")
    }

    test {
        failFast = true
        useJUnitPlatform()
    }

    pluginManager.withPlugin('kotlin') {

        dependencies {
            implementation "org.jetbrains.kotlin:kotlin-stdlib"
        }

        compileKotlin {
            kotlinOptions.useIR = true
            kotlinOptions.jvmTarget = "1.8"
            kotlinOptions.javaParameters = true
        }
        compileTestKotlin {
            kotlinOptions.jvmTarget = "1.8"
        }

    }

}

tasks.create('cleanTemp') {

    doLast {
        def temp = new File(System.getProperty("java.io.tmpdir"))
        temp.listFiles(new FilenameFilter() {
            @Override
            boolean accept(File dir, String name) {
                return dir == temp &&
                        name.startsWith("fmi4j_") ||
                        name.startsWith("cosim4j_") ||
                        name.startsWith("libcosim") ||
                        name.startsWith("vesselFmu") ||
                        name.startsWith("vico") ||
                        name.startsWith("fmu_proxy") ||
                        name.startsWith("proxy_fmu")
            }
        }).each {
            it.deleteDir()
        }
    }

}

clean.dependsOn(cleanTemp)
